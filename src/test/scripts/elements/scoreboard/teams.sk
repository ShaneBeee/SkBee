test "SkBee - Teams":
	before:
		spawn a sheep at event-location:
			set {_e} to entity
		# Starup new scoreboard
		suppress variable save warning
		set {_main} to main scoreboard
		set {_new} to new scoreboard
		set {_boards::*} to {_main} and {_new}

	test:
		# Test both the main scoreboard and a custom scoreboard
		loop {_boards::*}:
			set {_board} to loop-value

			# Register team
			set {_t} to team with id "blah_team" of {_board}
			assert team named "blah_team" of {_board} is registered with "Team 'blah_team' should be registered"
			assert {_t} is set with "The team should be set"

			# Name
			assert team name of {_t} is "blah_team" with "Team name should be 'blah_team'"
			assert team display name of {_t} = "blah_team" with "Team display name should match name at creation"
			set team display name of {_t} to "Blah Team"
			assert team display name of {_t} = "Blah Team" with "Team display name should now be 'Blah Team'"

			# Color
			assert team color of {_t} is not set with "Team color should not be set by default"
			set team color of {_t} to blue
			assert team color of {_t} = blue with "Team color should be blue now"

			# Prefix/Suffix
			assert team prefix of {_t} is not set with "Team shouldn't have a prefix yet"
			assert team suffix of {_t} is not set with "Team shouldn't have a suffix yet"
			set team prefix of {_t} to "some prefix"
			set team suffix of {_t} to "some suffix"
			assert team prefix of {_t} is "some prefix" with "Team prefix should be set now"
			assert team suffix of {_t} is "some suffix" with "Team suffix should be set now"

			# Team entries
			add {_e} to {_t}

			# UUID check - for some reason entity check wasn't working
			assert team entries as strings of {_t} contains uuid of {_e} with "The sheep should be on the team"

	after:
		# Cleanup
		delete all teams of {_main}
		delete all teams of {_new}
		delete entity within {_e}
		assert team "blah_team" of {_new} is not registered with "Team 'blah_team' should have been unregistered on the new scoreboard"
		assert team "blah_team" of {_main} is not registered with "Team 'blah_team' should have not been unregistered on the main scoreboard"
