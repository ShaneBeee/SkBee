test "SkBee - simple nbt compound":
    set {_n} to nbt from "{test:123}"

    # Test that a tag is set
    assert int tag "test" of {_n} is set with "int tag ""test"" should be set"

    # Test that the tag has a value of 123
    assert int tag "test" of {_n} = 123 with "tag ""test"" should have a value of 123"

    set string tag "string-test" of {_n} to "complete"

    # Test that the tag has a value of "complete"
    assert string tag "string-test" of {_n} = "complete" with "tag ""string-test"" should have a value of ""complete"""

    spawn a sheep at location(1, 100, 1)
    set {_e} to last spawned entity
    set {_n} to nbt of {_e}

    # Test a generic tag on an Entity
    assert byte tag "Bukkit.Aware" of {_n} is 1 with "tag ""Bukkit.Aware"" of nbt of entity should be 1"

    delete random entity of {_e}
    delete {_n}

    set {_i} to diamond sword with custom model data 1
    set {_n} to nbt of {_i}

    # Test that tag of item nbt is set
    assert int tag "minecraft:custom_model_data" of {_n} is set with "tag ""minecraft:custom_model_data"" of an item should be set"
    assert int tag "minecraft:custom_model_data" of {_n} = 1 with "tag ""minecraft:custom_model_data"" of an item should be 1"

    # Test that Pretty nbt works
    set {_pretty} to pretty nbt of {_n}
    assert {_pretty} is set with "Pretty NBT should have worked"

    # Test adding/removing to/from lists
    set {_n} to empty nbt compound

    add "hello" and "hi" to string list tag "string-list" of {_n}
    assert size of (string list tag "string-list" of {_n}) = 2 with "size of string-list should have been 2 after adding"

    add 1 and 2 to int list tag "int-list" of {_n}
    assert size of (int list tag "int-list" of {_n}) = 2 with "size of int-list should have been 2 after adding"

    remove "hi" from string list tag "string-list" of {_n}
    assert size of (string list tag "string-list" of {_n}) = 1 with "size of string-list should have been 1 after removing"

    remove 2 from int list tag "int-list" of {_n}
    assert size of (int list tag "int-list" of {_n}) = 1 with "size of int-list should have been 1 after removing"

test "SkBee - NBTCustomBlock":
    set {_l} to location(1,1,1)
    set block at {_l} to stone

    set string tag "test" of nbt of block at {_l} to "this is a test"
    assert size of nbt tags of nbt of block at {_l} = 1 with "size of nbt tags of block should be 1"

    delete tag "test" of nbt of block at {_l}

    assert size of nbt tags of nbt of block at {_l} = 0 with "size of nbt tags of block should be 0"

    assert size of nbt tags of nbt of chunk at {_l} = 0 with "size of nbt tags of chunk should be 0"
