import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.5'
    id 'maven-publish'
}

compileJava {
    options.encoding = 'UTF-8'
}

// SkBee version
version = '3.7.0'
def latestJava = 21
def oldestJava = 17

repositories {
    mavenCentral()
    mavenLocal()

    // Paper
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

    // Skript
    maven { url 'https://repo.skriptlang.org/releases' }

    // JitPack
    maven { url 'https://jitpack.io' }

    // CodeMC (NBT-API)
    maven { url 'https://repo.codemc.io/repository/maven-public/' }
}

dependencies {
    // Paper
    compileOnly("io.papermc.paper:paper-api:1.21.4-R0.1-SNAPSHOT")

    // Skript
    compileOnly(group: 'com.github.SkriptLang', name: 'Skript', version: 'dev~feature-SNAPSHOT') {
        transitive = false
    }

    // commons-io
    compileOnly("commons-io:commons-io:2.14.0")
    compileOnly("org.apache.commons:commons-text:1.10.0")

    // NBT-API
    implementation("de.tr7zw:item-nbt-api:2.14.1") {
        transitive = false
    }

    // FastBoard
    implementation("fr.mrmicky:fastboard:2.1.3")

    // Virtual Furnace
    implementation("com.github.ShaneBeeStudios:VirtualFurnace:1.1.1") {
        transitive = false
    }

    // bStats
    implementation(group: 'org.bstats', name: 'bstats-bukkit', version: '3.0.2')
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

build {
    dependsOn(shadowJar)
    tasks.withType(JavaCompile).tap {
        configureEach {
            options.compilerArgs.add("-Xlint:unchecked")
            options.compilerArgs.add("-Xlint:deprecation")
        }
    }
}

processResources {
    filesNotMatching("**/*.png") {
        filter ReplaceTokens, tokens: ["version": project.version]
    }
}

shadowJar {
    manifest {
        attributes "Main-Class" : "com.shanebeestudios.skbee.game.GamesMain"
    }

    archiveFileName = project.name + "-" + project.version + ".jar"
    relocate("de.tr7zw.changeme.nbtapi", "com.shanebeestudios.skbee.api.nbt")
    relocate("de.tr7zw.annotations", "com.shanebeestudios.skbee.api.nbt.annotations")
    relocate("fr.mrmicky.fastboard", "com.shanebeestudios.skbee.api.fastboard")
    relocate("com.shanebeestudios.vf", "com.shanebeestudios.skbee.api.virtualfurnace")
    relocate("org.bstats", "com.shanebeestudios.skbee.metrics")
    exclude('META-INF/**', 'LICENSE')
}

// Run task to build jar to another spot
tasks.register('server', Copy) {
    from shadowJar
    // Change this to wherever you want your jar to build
    into '/Users/ShaneBee/Desktop/Server/Skript/1-21-4/plugins'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId 'com.github.ShaneBeee'
            artifactId 'SkBee'
            version project.version
            artifact shadowJar
            artifact tasks.jar
        }
    }
}

javadoc {
    destinationDir = file("${projectDir}/build/javadoc")
    options {
        links('https://hub.spigotmc.org/javadocs/spigot/')
        links('https://docs.skriptlang.org/javadocs/')
        links('https://tr7zw.github.io/Item-NBT-API/v2-api/')
        links('https://jd.advntr.dev/api/4.14.0/')
    }
    options.addStringOption('Xdoclint:none', '-quiet')
    exclude("com/shanebeestudios/skbee/api/listener", "com/shanebeestudios/skbee/elements",
            "com/shanebeestudios/skbee/api/command", "com/shanebeestudios/skbee/game")
}
